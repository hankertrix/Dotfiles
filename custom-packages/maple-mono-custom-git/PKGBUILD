# Maintainer: Hanker
pkgname='maple-mono-custom-git'
pkgver=r629.99d507e
pkgrel=1
pkgdesc='A custom build of the Maple Mono font.'
arch=('any')
url='https://github.com/subframe7536/Maple-font'
license=('OFL-1.1')
depends=()
makedepends=('git' 'python' 'uv' 'jq')
provides=('maple-mono-custom')
conflicts=('maple-mono-custom')
source=("$pkgname::git+https://github.com/subframe7536/Maple-font.git")
md5sums=('SKIP')

pkgver() {
	cd "$pkgname" || exit 1
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$pkgname" || exit 1

	# Install the packages required
	uv sync

	# Modify the config file to use dotted zeroes,
	# enable double headed and reverse arrows,
	# enable alternate not equals '~=' symbol,
	# enable slashed z,
	# and enable chinese characters.
	edited_config=$(jq '.feature_freeze.zero = "enable"' config.json |
		jq '.feature_freeze.ss08 = "enable"' |
		jq '.feature_freeze.ss09 = "enable"' |
		jq '.feature_freeze.cv10 = "enable"' |
		jq '.feature_freeze.cv43 = "enable"' |
		jq '.cn.enable = true')

	# Write the edited config
	echo "$edited_config" >config.json

	# Build the TTF format with the configuration
	uv run build.py --ttf-only

	# Move the NF-CN folder out of the fonts directory
	mv fonts/NF-CN/ MapleMono-NF-CN

	# Build the TTF format with monospaced nerd fonts
	uv run build.py --ttf-only --nf-mono

	# Move the NF-CN folder out of the fonts directory
	mv fonts/NF-CN/ MapleMono-NFMono-CN

	# Build the TTF format with variable width glyphs
	uv run build.py --ttf-only --nf-propo

	# Move the NF-CN folder out of the fonts directory
	mv fonts/NF-CN/ MapleMono-NFPropo-CN
}

package() {
	cd "$pkgname" || exit 1

	# Install the licence
	install -d "$pkgdir/usr/share/licenses/maple-mono-custom-git"
	install -Dm644 -t "$pkgdir/usr/share/licenses/maple-mono-custom-git" OFL.txt

	# Iterate over each version of Maple Mono
	for version in "" "Mono" "Propo"; do

		# Install the font
		install -d "$pkgdir/usr/share/fonts/MapleMono-NF$version-CN"
		install -Dm644 -t "$pkgdir/usr/share/fonts/MapleMono-NF$version-CN" MapleMono-NF$version-CN/*.ttf
	done
}
